apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'


group = gradle.projectGroupId
version = gradle.projectVersion


sourceCompatibility = '1.8'

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    mavenLocal()
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.18'
    compile 'com.fasterxml.jackson.core:jackson-core:2.9.3'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.3'
    compile 'commons-io:commons-io:2.4'
    compile 'org.apache.httpcomponents:httpclient:4.5.4'
    compile 'org.apache.httpcomponents:httpmime:4.5.4'
    compile group: 'com.github.greatermkemeetup', name: 'slack-integrations-api', version: '1.0.4-SNAPSHOT', changing: true
    compile 'javax.websocket:javax.websocket-api:1.1'

    testCompile 'junit:junit:4.12'
	testCompile 'org.glassfish.tyrus.bundles:tyrus-standalone-client:1.13'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.allTasks.any { it instanceof Sign }) {
        allprojects { ext."signing.keyId" = ext."signing.keyId" ?: System.getenv("SIGNING_KEY_ID") }
        allprojects { ext."signing.secretKeyRingFile" = ext."signing.secretKeyRingFile" ?: System.getenv("SIGNING_KEY_PATH") }
        allprojects { ext."signing.password" = ext."signing.password" ?: System.getenv("SIGNING_KEY_PASSWORD") }
    }
}

signing {
    sign configurations.archives
}

def ossrhUsername = System.getenv("ossrhUsername") ?: ossrhUsername
def ossrhPassword = System.getenv("ossrhPassword") ?: ossrhPassword

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'com.github.greatermkemeetup:slack-integrations-core'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'An implementation of the slack-integrations-api.'
                url 'https://github.com/GreaterMKEMeetup/slack-integrations-core'

                scm {
                    connection 'scm:git:git://github.com/GreaterMKEMeetup/slack-integrations-core.git'
                    developerConnection 'scm:git:ssh://github.com:GreaterMKEMeetup/slack-integrations-core.git'
                    url 'https://github.com/GreaterMKEMeetup/slack-integrations-core'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id 'aglassman'
                        name 'Andrew Glassman'
                        email 'andrew.glassman@gmail.com'
                    }
                }
            }
        }
    }
}


build.dependsOn install
